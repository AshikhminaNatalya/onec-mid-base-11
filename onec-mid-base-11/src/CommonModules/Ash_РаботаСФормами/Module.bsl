   
#Область ПрограммныйИнтерфейс

Процедура ДополнитьФорму(Форма) Экспорт

	ИмяФормы = Форма.ИмяФормы;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);  
		ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
		ДобавитьКнопкуПересчитатьЦеныВГруппаШапкаЛево(Форма);
	ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда 
		ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма);
	ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда 
		ВставитьПолеКонтактноеЛицоНаФормуПередДоговор(Форма); 
    КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Ash_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма)
	
	ПолеВвода = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Ash_СогласованнаяСкидка"; 
	ПолеВвода.УстановитьДействие("ПриИзменении", "СогласованнаяСкидкаПриИзменении");
	
КонецПроцедуры  

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередОснование(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Основание);
    ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Ash_КонтактноеЛицо";
	
КонецПроцедуры 

Процедура ВставитьПолеКонтактноеЛицоНаФормуПередДоговор(Форма)
	
	ПолеВвода = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.Договор);
    ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Ash_КонтактноеЛицо";
	
КонецПроцедуры

Процедура ДобавитьКнопкуПересчитатьЦеныВГруппаШапкаЛево(Форма) 
	
	Команда = Форма.Команды.Добавить("ПересчитатьЦены");
	Команда.Заголовок = "Пересчитать цены";
	Команда.Действие = "КомандаПересчитатьЦены";
	
	КнопкаФормы = Форма.Элементы.Добавить("КомандаПересчитатьЦены", Тип("КнопкаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	КнопкаФормы.ИмяКоманды = "ПересчитатьЦены";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка;

КонецПроцедуры

#КонецОбласти

